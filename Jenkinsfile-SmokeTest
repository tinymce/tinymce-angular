#!groovy
@Library('waluigi@release/7') _

properties([
  disableConcurrentBuilds(),
  buildDiscarder(logRotator(
    artifactDaysToKeepStr: '',
    artifactNumToKeepStr: '1',
    daysToKeepStr: '',
    numToKeepStr: '50'
  )),
  parameters([
    string(defaultValue: 'Tinymce-Angular-SmokeTest', description: 'The test directory to run', name: 'TEST_DIR', trim: false),
    string(defaultValue: 'next', description: 'The test directory to run', name: 'TARGET_TAG', trim: false),
  ])
])

String customRouteFilePath = "${WORKSPACE}/version.json"

def runSmokeTests(Map args = new LinkedHashMap()) {

  def platforms = args.platforms ?: [
    [ browser: "chrome", provider: "lambdatest" ],
    [ browser: "firefox", provider: "lambdatest" ],
    [ browser: "safari", provider: "lambdatest" ],
    [ browser: "edge", provider: "lambdatest" ]
  ]

  def processes = [:]

  for (int i = 0; i < platforms.size(); i++) {
    def platform = platforms.get(i)
    def buckets = platform.buckets ?: 1

    for (int bucket = 1; bucket <= buckets; bucket++) {
      // clousure var - don't inline or jenkins complains
      def currBucket = bucket
      def suffix = currBucket == 1 ? '' : "-${currBucket}"

      if (platform.provider) {
        // Run using remote provider
        def name = "${platform.browser}-${platform.provider}${suffix}"
        // def testName = "TinyMCE-Angular-SmokeTest_${name}"
        processes[name] = {
          tinyPods.node {
            stage("${name}") {
              bedrockRemoteTools.tinyWorkSishTunnel()
              bedrockRemoteTools.withRemoteCreds(platform.provider) {
                def testStatus = exec(script: "yarn bedrock-auto -b ${platform.browser} -f tinymce-angular-component/src/test/ts/smoke-test/VerifyIntegrationTest.ts --chunk 20 --totalTimeout 1800000 --singleTimeout 90000 --retries 3 --remote lambdatest --customRoutes ${args.routeFile}", returnStatus: true)

                junit allowEmptyResults: true, testResults: 'scratch/TEST-*.xml'
                if (testStatus == 4) {
                  unstable("Tests failed for ${name}")
                } else if (testStatus != 0) {
                  error("Unexpected error running tests for ${name} so passing failure as exist code")
                }
              }
            }
          }
        }
      }
    }
  }

  parallel processes
}

timestamps {
  tinyPods.node(tag: '20') {
    stage("deps") {
      yarnInstall()
      def nextVersion = sh(script: 'npm view @angular/core@next version', returnStdout: true).trim()

      sh 'yarn add @angular/animations@next @angular/common@next @angular/compiler@next @angular/core@next @angular/forms@next @angular/platform-browser@next @angular-devkit/build-angular@next @angular/cli@next @angular/compiler-cli@next'

      def routeFileContent = readJSON text: """
        [{
          "request": {
            "method": "get",
            "path": "/custom/integration/info"
          },
          "response": {
            "status": 200,
            "json": {
              "version": "${nextVersion}"
            }
          }
        }]
      """
      writeJSON file: "${customRouteFilePath}", json: routeFileContent
    }

    stage("build") {
      sh 'yarn build'
    }

    stage("tests") {
      runSmokeTests(routeFile: customRouteFilePath)
    }
  }
}
